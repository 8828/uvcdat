# Find the Cairo and Pango includes and libraries, along with their dependencies
# The following variables are set if the proper things are.  
#  CAIRO_FOUND        - True when the Cairo include directory is found.
#  CAIRO_INCLUDE_DIRS - The path to where Cairo includes are
#  CAIRO_LIBRARIES    - Needed CAIRO libraries
#  PANGO_FOUND        - True when the Pango include directory is found.
#  PANGO_INCLUDE_DIRS - The path to where pango includes are
#  PANGO_LIBRARIES    - Needed pango libraries

# Usage:
# In your CMakeLists.txt file do something like this:
# ...
# # CAIRO
# FIND_PACKAGE(CAIRO)
# ...
# INCLUDE_DIRECTORIES(${CAIRO_INCLUDE_DIRS})
# LINK_DIRECTORIES(${PANGO_LIBRARY_DIRS})
#
# First, let's check to see if user has pkg-config set up to find these
IF(NOT PANGO_INCLUDE_DIR AND NOT CAIRO_INCLUDE_DIR )
  find_package( PkgConfig )
  pkg_check_modules( PANGO pango pangocairo )
  pkg_check_modules( CAIRO cairo>=1.7.4 )
ENDIF(NOT PANGO_INCLUDE_DIR AND NOT CAIRO_INCLUDE_DIR )

IF(NOT PANGO_FOUND AND NOT CAIRO_FOUND)

  IF(WIN32)
    SET(CAIRO_INCLUDE_SEARCH 
      C:/gtk/include
    )
    SET(CAIRO_LIB_SEARCH
      C:/gtk/bin
    )
    SET(CAIRO_LIBRARY_NAME
      cairo.lib
    )
    SET(PANGO_LIBRARY_NAME
      pango-1.0.lib
    )
  ELSE(WIN32)
    SET(CAIRO_INCLUDE_SEARCH
      /usr/local/include/
      /usr/include/
    )
    SET(CAIRO_LIB_SEARCH
      /usr/local/lib/
      /usr/lib
    )
    SET(CAIRO_LIBRARY_NAME
      libcairo.so
    )
    SET(PANGO_LIBRARY_NAME
      libpango-1.0.so
    )
  ENDIF(WIN32)

  IF(NOT CAIRO_INCLUDE_DIRS)
    FIND_PATH(CAIRO_INCLUDE_DIRS cairo/cairo.h
      ${PANGO_INCLUDE_DIRS}
      ${CAIRO_INCLUDE_SEARCH}
      DOC "Location of cairo/cairo.h")
  ENDIF(NOT CAIRO_INCLUDE_DIRS)
  
  IF(CAIRO_INCLUDE_DIRS)
    SET(BASE_INCLUDE_DIR ${CAIRO_INCLUDE_DIRS})
    SET(CAIRO_INCLUDE_DIRS ${CAIRO_INCLUDE_DIRS}/cairo)
  ENDIF(CAIRO_INCLUDE_DIRS)

  IF(NOT CAIRO_LIBRARY_DIRS)
    FIND_PATH(CAIRO_LIBRARY_DIRS ${CAIRO_LIBRARY_NAME}
      ${CAIRO_LIB_SEARCH}
      DOC "Directory that holds ${CAIRO_LIBRARY_NAME}"
      )
  ENDIF(NOT CAIRO_LIBRARY_DIRS)

  IF(NOT PANGO_LIBRARY_DIRS)
    FIND_PATH(PANGO_LIBRARY_DIRS ${PANGO_LIBRARY_NAME}
      ${CAIRO_LIBRARY_DIRS}
      DOC "Directory that holds ${PANGO_LIBRARY_NAME}"
      )
  ENDIF(NOT PANGO_LIBRARY_DIRS)

  IF(CAIRO_LIBRARY_DIRS)
    IF(NOT CAIRO_LIBRARY)
      FIND_LIBRARY(CAIRO_LIBRARY
  NAMES cairo
  PATHS ${CAIRO_LIBRARY_DIRS} ${CAIRO_LIB_SEARCH}
  )
    ENDIF(NOT CAIRO_LIBRARY)
  ENDIF(CAIRO_LIBRARY_DIRS)

  IF(BASE_INCLUDE_DIR)
    IF(NOT PANGO_INCLUDE_DIRS)
      FIND_PATH(PANGO_INCLUDE_DIRS pango-1.0/pango/pangocairo.h
  ${BASE_INCLUDE_DIR}
  ${CAIRO_INCLUDE_SEARCH}
  DOC "Location of directory pango-1.0/pango")
    ENDIF(NOT PANGO_INCLUDE_DIRS)
  ENDIF(BASE_INCLUDE_DIR)

  IF(PANGO_INCLUDE_DIRS)
    SET(PANGO_INCLUDE_DIRS ${PANGO_INCLUDE_DIRS}/pango-1.0)
  ENDIF(PANGO_INCLUDE_DIRS)    

  IF(PANGO_LIBRARY_DIRS)
    IF(NOT PANGO_LIBRARY)
      FIND_LIBRARY(PANGO_LIBRARY
  NAMES pango-1.0
  PATHS ${PANGO_LIBRARY_DIRS} ${CAIRO_LIB_SEARCH}
  )
    ENDIF(NOT PANGO_LIBRARY)
  ENDIF(PANGO_LIBRARY_DIRS)

  IF(PANGO_LIBRARY)
    IF(NOT PANGOCAIRO_LIBRARY)
      FIND_LIBRARY(PANGOCAIRO_LIBRARY
  NAMES pangocairo-1.0
  PATHS ${PANGO_LIBRARY_DIRS} ${CAIRO_LIB_SEARCH}
  )
    ENDIF(NOT PANGOCAIRO_LIBRARY)
  ENDIF(PANGO_LIBRARY)

  IF(BASE_INCLUDE_DIR)
    IF(NOT GLIB_INCLUDE_DIRS)
      FIND_PATH(GLIB_INCLUDE_DIRS glib.h
  ${BASE_INCLUDE_DIR}/glib-2.0
  ${CAIRO_INCLUDE_SEARCH}
  DOC "Location of directory glib-2.0"
  )
    ENDIF(NOT GLIB_INCLUDE_DIRS)
  ENDIF(BASE_INCLUDE_DIR)

  IF(PANGO_LIBRARY_DIRS)
    IF(NOT GLIBCONFIG_INCLUDE_DIRS)
      FIND_PATH(GLIBCONFIG_INCLUDE_DIRS glib-2.0/include/glibconfig.h
  ${PANGO_LIBRARY_DIRS}
  /usr/local/lib/glib-2.0/include
  /usr/lib/glib-2.0/include
  DOC "Base of directory glib-2.0/include/glibconfig.h"
  )
    ENDIF(NOT GLIBCONFIG_INCLUDE_DIRS)
  ENDIF(PANGO_LIBRARY_DIRS)

  IF(GLIBCONFIG_INCLUDE_DIRS)
    SET(GLIBCONFIG_INCLUDE_DIRS ${GLIBCONFIG_INCLUDE_DIRS}/glib-2.0/include)
  ENDIF(GLIBCONFIG_INCLUDE_DIRS)

  IF(PANGO_INCLUDE_DIRS)
    IF(GLIB_INCLUDE_DIRS)
      SET(PANGO_INCLUDE_DIRS 
  ${PANGO_INCLUDE_DIRS} 
  ${GLIB_INCLUDE_DIRS}
  )
    ENDIF(GLIB_INCLUDE_DIRS)
    IF(GLIBCONFIG_INCLUDE_DIRS)
      SET(PANGO_INCLUDE_DIRS 
  ${PANGO_INCLUDE_DIRS} 
  ${GLIBCONFIG_INCLUDE_DIRS}
  )
    ENDIF(GLIBCONFIG_INCLUDE_DIRS)
  ENDIF(PANGO_INCLUDE_DIRS)

  IF(PANGOCAIRO_LIBRARY AND PANGO_LIBRARY)
    SET(PANGO_LIBRARIES
      ${PANGO_LIBRARY}
      ${PANGOCAIRO_LIBRARY}
      )
  ENDIF(PANGOCAIRO_LIBRARY AND PANGO_LIBRARY)

  IF(CAIRO_LIBRARY)
    SET(CAIRO_LIBRARIES
      ${CAIRO_LIBRARY}
      )
  ENDIF(CAIRO_LIBRARY)

  IF(PANGO_INCLUDE_DIRS AND PANGO_LIBRARY)
    SET(PANGO_FOUND 1)
  ENDIF(PANGO_INCLUDE_DIRS AND PANGO_LIBRARY)

  IF(CAIRO_INCLUDE_DIRS AND CAIRO_LIBRARY)
    SET(CAIRO_FOUND 1)
  ENDIF(CAIRO_INCLUDE_DIRS AND CAIRO_LIBRARY)

ENDIF(NOT PANGO_FOUND AND NOT CAIRO_FOUND)

